import "syscall.csp";
import "io.csp";
import "option.csp";
import "utils.csp";
import "c_str.csp";

namespace std {
    type File: struct {
        desc: i32,
        mode: u8,
        path: &char,
        offset: u64
    };

    namespace file {
        const READ: u8       = 0b0001;
        const WRITE: u8      = 0b0010;
        const READ_WRITE: u8 = 0b0011;
        const CREATE: u8     = 0b0100;

        fn open(path: const &char, mode: u8): &std::File
        {
            let file: &File = mem::alloc(sizeof File);
            file.mode = mode;
            file.offset = 0;
            
            let sys_flags = 0;

            if mode & CREATE
                sys_flags |= io::O_CREAT;
            if mode & READ_WRITE
                sys_flags |= io::O_RDWR;
            else if mode & READ
                sys_flags |= io::O_RDONLY;
            else if mode & WRITE 
                sys_flags |= io::O_WRONLY;
            else
                <- nil;

            file.desc = syscall::open(path, sys_flags, 416);
            if file.desc <= -1
                <- nil;

            <- file;
        }

        fn write_str(file: &File, str: const &char)
            write(file, str: const &void, c_str::strlen(str));

        fn write(file: &File, data: const &void, count: usize)
        {
            syscall::write(file.desc, data, count);
        }

        fn read(file: &File, buffer: &void, count: usize)
        {
            syscall::read(file.desc, buffer, count);
        }

        fn readc(file: &File): char 
        {
            if file == nil 
                ret -1;

            let c = '\0';
            syscall::pread64(file.desc, &c, 1, file.offset);
            file.offset++;
            <- c;
        }

        fn close(file: &File)
        {
            syscall::close(file.desc);
            free!(file);
        }
        [exit_fn("close": &std::File)]

        fn size(file: &File): i64
        {
            let stat: Stat;
            if !file
                <- -1;
            if syscall::fstat(file.desc, &stat)
                <- -1;

            <- stat.size;
        }

        fn read_all(file: &File): String
        {
            let file_size = size(file);
            let str = string::init_sized(file_size);
            read(file, str, file_size);

            <- str;
        }
    }
}