import "syscall.csp";
import "io.csp";
import "option.csp";
import "utils.csp";
import "c_str.csp";
import "error.csp";
import "str.csp";

namespace std {
    type File: struct {
        desc: i32,
        mode: u8,
        path: &char,
        offset: u64,
    };

    namespace file {
        const READ: u8       = 0b0001;
        const WRITE: u8      = 0b0010;
        const READ_WRITE: u8 = 0b0011;
        const CREATE: u8     = 0b0100;

        fn open(path: &const char, mode: u8): &std::File
        {
            let file: &File = mem::alloc(sizeof File);
            file.mode = mode;
            file.offset = 0;
            
            let sys_flags = 0;

            if mode & CREATE
                sys_flags |= io::O_CREAT;
            if mode & READ_WRITE
                sys_flags |= io::O_RDWR;
            else if mode & READ
                sys_flags |= io::O_RDONLY;
            else if mode & WRITE 
                sys_flags |= io::O_WRONLY;
            else {
                error::new(std::ErrorType::INVAL, "invalid mode for `std::file::open()`");
                <- nil;
            }

            file.desc = syscall::open(path, sys_flags, 416);
            if file.desc < 0 {
                error::new(std::ErrorType::IO, "could not write file");
                <- nil;
            }

            <- file;
        }

        fn write_str(file: &File, str: const &char)
            write(file, str: const &void, c_str::strlen(str));

        fn write(file: &File, data: const &void, count: usize): u64
        {
            let written = syscall::write(file.desc, data, count);
            if written == -1 
                error::new(std::ErrorType::IO, "could not write to file");
            <- written;
        }

        fn read(file: &File, buffer: &void, count: usize): &void
        {
            if syscall::read(file.desc, buffer, count) == -1 
                error::new(std::ErrorType::IO, "could not read file");
            <- buffer;
        }

        fn readc(file: &File): char 
        {
            if file == nil
                ret -1;

            let c = '\0';
            if syscall::read(file.desc, &c, 1) == -1 
                error::new(std::ErrorType::IO, "could not read file");
            file.offset++;
            <- c;
        }

        fn close(file: &File)
        {
            if syscall::close(file.desc) == -1
                error::new(std::ErrorType::IO, "could not close file");
            free!(file);
        }
        [exit_fn("close": &std::File)]

        fn size(file: &File): i64
        {
            let stat: Stat;
            if !file || syscall::fstat(file.desc, &stat)
            {
                error::new(std::ErrorType::IO, "could not get statistics of file");
                <- -1;
            }

            <- stat.size;
        }

        fn read_all(file: &File): String
        {
            <- size(file) |> read(file, string::init_sized($), $);
        }

        const SEEK_SET: u8 = 0;
        const SEEK_CUR: u8 = 1; 
        const SEEK_END: u8 = 2;

        fn seek(file: &File, offset: i64, whence: u8): Error
        {
            do 
                ret error::new(std::ErrorType::NILPTR, "`file` argument is nil")
            unless file;

            match whence {
                SEEK_SET => file.offset = offset;
                SEEK_CUR => file.offset += offset;
                SEEK_END => file.offset = size(file) - offset - 1;
                _ => ret error::new(std::ErrorType::INVAL, "value of `whence` is invalid");
            }

            syscall::lseek(file.desc, file.offset, SEEK_SET);
            <- error::none();
        }

        fn tell(file: &File): i64 
        {
            do {
                error::new(std::ErrorType::NILPTR, "`file` argument is nil");
                <- -1;
            } unless file;
            <- file.offset;
        } 

        fn rewind(file: &File): Error
        {
            do
                ret error::new(std::ErrorType::NILPTR, "`file` argument is nil")
            unless file;
            file.offset = 0;
            syscall::lseek(file.desc, 0, SEEK_SET);
            <- error::none();
        }
    }
}