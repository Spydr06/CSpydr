import "error.csp";
import "io.csp";
import "string.csp";

namespace std {
    type Flag: struct {
        func: fn(&void),
        name: &const char,
        desc: &const char,
        kind: flag::Kind,
    };

    namespace flag {
        type Kind: enum {
            DEFAULT,
            STRING,
            BOOL,
            INT,
            HELP
        };

        fn new(name: &const char, desc: &const char, func: fn): Flag 
        {            
            <- Flag::{
                func: fn(&void),
                name,
                desc,
                Kind::DEFAULT
            };
        }

        fn new_str(name: &const char, desc: &const char, func: fn(&const char)): Flag
        {
            <- Flag::{
                func: fn(&void),
                name,
                desc,
                Kind::STRING
            };
        }

        fn new_bool(name: &const char, desc: &const char, func: fn(bool)): Flag
        {
            <- Flag::{
                func: fn(&void),
                name,
                desc,
                Kind::BOOL
            };
        }

        fn new_int(name: &const char, desc: &const char, func: fn(i64)): Flag
        {
            <- Flag::{
                func: fn(&void),
                name,
                desc,
                Kind::INT
            };
        }

        fn help(desc: &const char): Flag
        {
            <- Flag::{
                nil: fn(&void),
                "--help",
                desc,
                Kind::HELP
            };
        }
    }

    namespace flags {
        fn parse(flags: const Flag[], argv: &&char, argc: i32): Error
        {
            for let i = 1; i < argc; i++; {
                let arg = argv[i];
                let matched = false;

                for let j = 0; j < len flags; j++; {
                    let flg = flags[j];

                    if string::equal(flg.name, arg) {
                        matched = true;

                        match flg.kind {
                            std::flag::Kind::DEFAULT => {
                                flg.func(nil);
                            }
                            std::flag::Kind::HELP => {
                                __internal::handle_help(&flags[j], flags);
                            }
                            std::flag::Kind::STRING => {
                                if (i += 1) >= argc {
                                    using io::color;
                                    io::eprintf("%sExpect <string> after `%s`.%s\n", &RED[0], arg, &RESET[0]);
                                    <- error::new(std::Errno::INVAL, "expected string");
                                }
                                flg.func(argv[i]);
                            }
                            std::flag::Kind::BOOL => {
                                if (i += 1) >= argc {
                                    using io::color;
                                    io::eprintf("%sExpect [true|false] after `%s`.%s\n", &RED[0], arg, &RESET[0]);
                                    <- error::new(std::Errno::INVAL, "expected bool");
                                }
                                let bool_val = false;
                                if string::equal(argv[i], "true")
                                    bool_val = true;
                                else if !string::equal(argv[i], "false")
                                {
                                    using io::color;
                                    io::eprintf("%sExpect [true|false] after `%s`, got %s.%s\n", &RED[0], arg, argv[i], &RESET[0]);
                                    <- error::new(std::Errno::INVAL, "expected bool");
                                }
                                flg.func(bool_val: &void);
                            }
                            std::flag::Kind::INT => {
                                if (i += 1) >= argc {
                                    using io::color;
                                    io::eprintf("%sExpect <integer> after `%s`.%s\n", &RED[0], arg, &RESET[0]);
                                    <- error::new(std::Errno::INVAL, "expected int");
                                }
                                let int = c_str::to_i64(argv[i]);
                                flg.func(int: &void);
                            }
                            _ => {}
                        }

                        break;
                    }
                }

                if !matched {
                    using io::color;
                    io::eprintf("%sUnexpected argument `%s`.%s\n", &RED[0], arg, &RESET[0]);
                    <- error::new(std::Errno::INVAL, "unexpected argument");
                }
            }

            <- error::none();
        }

        namespace __internal {
            fn handle_help(help_flag: &Flag, flags: const Flag[]) {
                using io;
                do 
                    printf("%s\n", help_flag.desc) 
                unless string::equal(help_flag.desc, "");


                for let i = 0; i < len flags; i++; {
                    let args: &char = "";
                    match flags[i].kind {
                        std::flag::Kind::STRING => args = " <string>";
                        std::flag::Kind::BOOL =>   args = " [true|false]";
                        std::flag::Kind::INT =>    args = " <integer>";
                        _ => {}
                    }
                    if &flags[i] == help_flag
                        printf("  %s:\tDisplays this help text.\n", flags[i].name);
                    else
                        printf("  %s%s:\t%s\n", flags[i].name, args, flags[i].desc);
                }
            }
        }
    }
}